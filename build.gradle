def gitCommitHash = {
    def cmd = "git rev-parse --short HEAD"
    try {
        def ver = cmd.execute().text.trim()
        if (ver == "") return ""
        return "+" + ver
    } catch (Exception e) {
        e.printStackTrace()
        return ""
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/releases/"
        }
    }
}

subprojects {
    apply plugin: 'java-library'

    group = "me.zhenxin"
    version = "3.2.0"

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.26")
        annotationProcessor("org.projectlombok:lombok:1.18.26")
    }

    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from {
            //noinspection GroovyMissingReturnStatement
            configurations.runtimeClasspath.collect {
                if (project.name != "zmusic-core") {
                    if (it.name.contains("zmusic-core")) {
                        it.isDirectory() ? it : zipTree(it)
                    }
                }
            }
        }

        from("${rootDir}/LICENSE")

        if (project.name.contains("zmusic-forge-1")) {
            manifest {
                attributes([
                        "Specification-Title"     : "zmusic",
                        "Specification-Vendor"    : "ZhenXin",
                        "Specification-Version"   : "1",
                        "Implementation-Title"    : "ZMusic Mod",
                        "Implementation-Version"  : project.version,
                        "Implementation-Vendor"   : "ZhenXin",
                        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
                ])
            }
        }
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }

        filesMatching("META-INF/mods.toml") {
            expand "version": project.version
        }

        filesMatching("mcmod.info") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
}